open PageComponents

let frenchLaw = %raw(`require("../../assets/french_law.js")`)

let englishSchema = %raw(`require("../../assets/allocations_familiales_schema_en.json")`)
let frenchSchema = %raw(`require("../../assets/allocations_familiales_schema_fr.json")`)

let englishUiSchema = %raw(`require("../../assets/allocations_familiales_ui_schema_en.json")`)
let frenchUiSchema = %raw(`require("../../assets/allocations_familiales_ui_schema_fr.json")`)

let title =
  <Lang.String
    english="French family benefits computation" french=`Calcul des allocations familiales`
  />

let card: Card.Presentation.t = {
  title: <Lang.String english="French family benefits" french="Allocations familiales" />,
  action: Some((
    Internal([Nav.home, Nav.examples, Nav.frenchFamilyBenefitsExample]),
    <Lang.String english="See example" french=`Voir l'exemple` />,
  )),
  icon: None,
  quote: None,
  content: <>
    <Lang.String
      english="The content of the example is generated by the Catala compiler from the "
      french=`Le contenu de cet exemple est généré par le compilateur Catala à partir des `
    />
    <Link.Text
      target="https://github.com/CatalaLang/catala/tree/master/examples/allocations_familiales">
      <Lang.String english="source code files of the example" french=`sources de l'exemple` />
    </Link.Text>
    <Lang.String
      english=". The code, like the legislative text it follows, is written in French." french="."
    />
  </>,
}

module Simulator = {
  let pageTitle = title

  module ComputationResult = {
    @react.component
    let make = (
      ~formData: option<Js.Json.t>,
      ~setEventsOpt: (option<array<LogEvent.event>> => option<array<LogEvent.event>>) => unit,
    ) => {
      {
        React.useEffect2(() => {
          setEventsOpt(_ => {
            let logs = retrieveEventsSerialized()->LogEvent.deserializedEvents
            if 0 == logs->Belt.Array.size {
              None
            } else {
              Some(logs)
            }
          })
          None
        }, (formData, setEventsOpt))
      }

      <div
        className=%twc(
          "w-full inline-flex flex-col justify-center place-items-center \
          my-4 border border-gray border-solid rounded p-4 shadow-sm \
          bg-gray_light text-gray_dark shadow"
        )>
        {switch computeAllocationsFamiliales(formData) {
        | Error(msg) => <div className=%twc("font-bold")> msg </div>
        | Result(amount) => <>
            <div className=%twc("pr-2 font-semibold")>
              <Lang.String
                english="Family benefits monthly amount:"
                french=`Montant mensuel des allocations familiales :`
              />
            </div>
            <div className=%twc("flex flex-row justify-center")>
              <div className=%twc("font-bold whitespace-nowrap")>
                <span className=%twc("text-mb font-mono")> {React.float(amount)} </span>
                {React.string(` €`)}
              </div>
            </div>
          </>
        }}
      </div>
    }
  }

  @react.component
  let make = (
    ~setEventsOpt: (option<array<LogEvent.event>> => option<array<LogEvent.event>>) => unit,
  ) => {
    let (formData, setFormData) = React.useState(_ => {
      None
    })

    <>
      <Form.FromJSONSchema
        schema={Lang.getCurrent(~english=englishSchema, ~french=frenchSchema)}
        uiSchema={Lang.getCurrent(~english=englishUiSchema, ~french=frenchUiSchema)}
        formData={formData->Belt.Option.getWithDefault(Js.Json.null)}
        onSubmit={t => setFormData(_ => t->Js.Dict.get("formData"))}
        onChange={t => Js.log("[CHANGED]")}
        onError={_ => Js.log("error")}
      />
      <ComputationResult formData setEventsOpt />
    </>
  }
}

module Visualizer = Visualization.Make(Simulator)

@react.component
let make = () => {
  <>
    <Title> title </Title>
    <p>
      <Lang.String
        english="The source code for this example is available "
        french=`Le code source de cet exemple est disponible `
      />
      <Link.Text
        target="https://github.com/CatalaLang/catala/tree/master/examples/allocations_familiales">
        <Lang.String english="here" french=`ici` />
      </Link.Text>
      <Lang.String
        english=". What you can see here is the \"weaved\" output of the source files processed by the Catala compiler.
        Weaving is a concept from "
        french=`. Ce que vous pouvez voir en dessous est la version "tissée" des fichiers sources transformés par le compilateur Catala.
        Le tissage est un concept issu de la `
      />
      <Link.Text target="https://en.wikipedia.org/wiki/Literate_programming#Workflow">
        <Lang.String english="literate programming" french=`programmation littéraire` />
      </Link.Text>
      <Lang.String
        english=" corresponding to the action of interleaving together the code and its textual documentation
         as to produce a reviewable and comprehensive document. Please refer to the tutorial for a hands-on introduction
          on how to read this document."
        french=` , qui correspond à l'action d'entremêler le code et sa documentation textuelle dans un document
         complet et lisible. Veuillez vous réferer au tutoriel pour savoir comment lire ce document.`
      />
    </p>
    <Section title={<Lang.String english="Form" french=`Formulaire` />}>
      <Simulator setEventsOpt={_ => ()} />
    </Section>
    <div className=%twc("inline-flex justify-end")>
      <Button.Internal
        target={[Nav.home, Nav.examples, Nav.frenchFamilyBenefitsExample, Nav.visualization]}>
        <Icon className=%twc("pr-2") name="explore" />
        <Lang.String
          english="Explore the execution trace" french=`Explorer la trace d'exécution`
        />
      </Button.Internal>
    </div>
    <Section title={<Lang.String english="Source code" french=`Code source` />}>
      <CatalaCode.DangerouslySetInnerHtml
        html=%raw(`require("../../assets/allocations_familiales.html")`)
      />
    </Section>
  </>
}
